plugins {
    id "org.owasp.dependencycheck" version "5.3.2"
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.25.0'
    id 'com.github.ben-manes.versions' version '0.8'
    id 'checkstyle'
}

def jettyVersion = "9.4.25.v20191220"
def springVersion = "5.2.3.RELEASE"
def eclipselinkVersion = "2.7.7"
def jerseyVersion = "2.27"
def slf4jVersion = "1.7.5"
def logbackVersion = "1.2.3"
allprojects {
    group = 'com.melowe.tessera'

    plugins.withType(JavaPlugin).whenPluginAdded {

        dependencies {
            constraints {
                implementation "org.slf4j:slf4j-api:"+ slf4jVersion
                runtimeOnly "org.slf4j:jcl-over-slf4j:"+ slf4jVersion
                runtimeOnly "org.slf4j:jul-to-slf4j:"+ slf4jVersion
                runtimeOnly "ch.qos.logback:logback-classic:"+ logbackVersion
                runtimeOnly "ch.qos.logback:logback-core:"+ logbackVersion

                runtimeOnly "org.eclipse.persistence:org.eclipse.persistence.moxy:"+ eclipselinkVersion
                implementation "org.eclipse.persistence:org.eclipse.persistence.jpa:"+ eclipselinkVersion
                implementation "org.eclipse.persistence:org.eclipse.persistence.extension:"+ eclipselinkVersion

                testImplementation "junit:junit:4.13"
                testImplementation "org.assertj:assertj-core:3.9.1"
                testImplementation "org.mockito:mockito-core:2.28.2"
                testImplementation "com.openpojo:openpojo:0.8.10"
                testImplementation "com.github.stefanbirkner:system-rules:1.19.0"
                testImplementation "nl.jqno.equalsverifier:equalsverifier:3.1.5"
                testImplementation "com.mockrunner:mockrunner-jdbc:2.0.4"

                implementation "commons-cli:commons-cli:1.4"
                implementation "commons-codec:commons-codec:1.6"
                implementation "commons-io:commons-io:2.6"
                implementation "org.apache.commons:commons-lang3:3.7"

                implementation "com.github.jnr:jnr-unixsocket:0.28"
                implementation "de.mkammerer:argon2-jvm:2.5"

                implementation "info.picocli:picocli:4.0.4"
                implementation "org.jasypt:jasypt:1.9.3"
                implementation "com.moandjiezana.toml:toml4j:0.7.2"
                implementation "org.bouncycastle:bcpkix-jdk15on:1.61"

                implementation "org.glassfish.jersey.inject:jersey-hk2:"+ jerseyVersion
                implementation "org.glassfish.jersey.media:jersey-media-json-processing:"+ jerseyVersion
                implementation "org.glassfish.jersey.media:jersey-media-moxy:"+ jerseyVersion
                implementation "org.glassfish.jersey.test-framework:jersey-test-framework-core:"+ jerseyVersion
                implementation "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:"+ jerseyVersion
                implementation "org.glassfish.jersey.media:jersey-media-moxy:"+ jerseyVersion
                implementation "org.glassfish.jersey.core:jersey-server:"+ jerseyVersion
                implementation "org.glassfish.jersey.ext:jersey-bean-validation:"+ jerseyVersion
                implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:"+ jerseyVersion
                implementation "org.glassfish.jersey.inject:jersey-hk2:"+ jerseyVersion
                implementation "org.glassfish.jersey.core:jersey-common:"+ jerseyVersion
                implementation "org.glassfish.jersey.core:jersey-client:"+ jerseyVersion

                implementation "org.eclipse.jetty:jetty-unixsocket:"+ jettyVersion
                implementation "org.eclipse.jetty:jetty-client:"+ jettyVersion
                implementation "org.eclipse.jetty:jetty-servlet:"+ jettyVersion
                implementation "org.eclipse.jetty:jetty-unixsocket:"+ jettyVersion
                implementation "org.eclipse.jetty:jetty-server:"+ jettyVersion

                implementation "org.cryptacular:cryptacular:1.2.2"
                implementation "eu.neilalexander:jnacl:1.0.0"

                implementation "io.swagger:swagger-annotations:1.5.4"
                implementation "org.bouncycastle:bcpkix-jdk15on:1.61"

                implementation "org.springframework:spring-orm:"+ springVersion
                testCompile "org.springframework:spring-test:"+ springVersion
                implementation "org.springframework:spring-core:"+ springVersion
                implementation "org.springframework:spring-beans:"+ springVersion
                implementation "org.springframework:spring-context:"+ springVersion
                implementation "org.springframework:spring-orm:"+ springVersion

                implementation "org.bouncycastle:bcprov-jdk15on:1.61"

                implementation "com.h2database:h2:1.4.200"
                implementation "com.zaxxer:HikariCP:3.2.0"
                implementation "org.hsqldb:hsqldb:2.4.1"
                implementation "org.xerial:sqlite-jdbc:3.23.1"

                implementation "javax.ws.rs:javax.ws.rs-api:2.1"
                implementation "javax.persistence:javax.persistence-api:2.2"
                implementation "javax.inject:javax.inject:1"
                implementation "javax.xml.bind:jaxb-api:2.3.0"
                implementation "javax.activation:javax.activation-api:1.2.0"
                implementation "javax.annotation:javax.annotation-api:1.3.2"
                implementation "javax.transaction:javax.transaction-api:1.3"
                implementation "javax.servlet:javax.servlet-api:4.0.1"
                implementation "com.sun.mail:javax.mail:1.6.2"

                implementation "org.glassfish.jaxb:jaxb-runtime:2.3.0"
                implementation "org.glassfish:jsonp-jaxrs:1.1.6"
                implementation "org.glassfish:javax.json:1.1.2"
                implementation "org.glassfish:javax.el:3.0.1-b10"

                implementation "javax.validation:validation-api:2.0.1.Final"
                implementation "org.hibernate:hibernate-validator:6.0.2.Final"

            }
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'org.owasp.dependencycheck'

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    dependencies {

        implementation 'org.slf4j:slf4j-api'
        runtimeOnly 'ch.qos.logback:logback-classic'
        runtimeOnly 'ch.qos.logback:logback-core'
        runtimeOnly 'org.eclipse.persistence:org.eclipse.persistence.moxy'

        testCompile 'junit:junit'
        testCompile 'org.assertj:assertj-core'
        testCompile 'org.mockito:mockito-core'
        testCompile 'com.openpojo:openpojo'
        testCompile 'com.github.stefanbirkner:system-rules'
        testCompile 'nl.jqno.equalsverifier:equalsverifier'
    }

    test {
        systemProperty("javax.xml.bind.JAXBContextFactory", "org.eclipse.persistence.jaxb.JAXBContextFactory")
        systemProperty("javax.xml.bind.context.factory", "org.eclipse.persistence.jaxb.JAXBContextFactory")
    }
    checkstyle {
        configFile project.getRootProject().file('checkstyle.xml')
        configProperties.put('checkstyle.suppressions.file',project.getRootProject().file('checkstyle-suppressions.xml'))
        dependencies{
            checkstyle "com.puppycrawl.tools:checkstyle:8.30"
            checkstyle "com.github.sevntu-checkstyle:sevntu-checks:1.37.1"
        }
    }

    dependencyCheck {
        failBuildOnCVSS = 0
        suppressionFile = 'cvss-suppressions.xml'


    }
    
    jacoco {
        toolVersion = "0.8.5"
    }

    spotless {

        lineEndings = 'unix'

        java {
            target fileTree('**/*.java')
            // indentWithSpaces(4)
            removeUnusedImports()
            // googleJavaFormat().aosp()
        }
        groovyGradle {
            target '*.gradle'
            indentWithSpaces(4)
            paddedCell()
        }
    }
    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
        }
    }
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 1.0
                }
                limit {
                    counter = 'INSTRUCTION'
                    value = 'COVEREDRATIO'
                    minimum = 1.0
                }

                excludes = [
                        'com.quorum.tessera.config.migration.Main',
                        'com.quorum.tessera.data.migration.Main',
                        'com.quorum.tessera.passwords.ConsolePasswordReader',
                        'com.quorum.tessera.passwords.PasswordReaderFactory',
                        'com.quorum.tessera.enclave.rest.Main',
                        'com.quorum.tessera.key.vault.azure.AzureKeyVaultClientDelegate',
                        'com.quorum.tessera.key.vault.hashicorp.KeyValueOperationsDelegateFactory'
                ]
            }

        }
    }



    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/resources'
            }
        }
        test {
            java {
                srcDir 'src/test/java'

            }
            resources {
                srcDir 'src/test/resources'
            }
        }
    }

    sourceCompatibility = '11'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError false
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    publishing {

        publications {

            mavenJava(MavenPublication) {

                from components.java
                afterEvaluate {
                   // artifactId = jar.archiveBaseName
                }

                pom {
                    groupId = 'com.jpmorgan.quorum'
//                    description = 'A concise description of my library'
                    url = 'https://github.com/melowe/tessera'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'melowe'
                            name = 'Mark Lowe'
                            email = 'melowe.quorum@gmail.com'
                        }
                        developer {
                            id = 'prd-fox'
                            name = 'Peter Fox'
                            email = 'peter.rd.fox@gmail.com'
                        }
                        developer {
                            id = 'namtruong'
                            name = 'Nam Truong'
                            email = 'nam.p.truong@gmail.com'
                        }
                        developer {
                            id = 'SatpalSandhu61'
                            name = 'Satpal Sandhu'
                            email = 'quorum@satpal.co.uk'
                        }
                        developer {
                            id = 'chrishounsom'
                            name = 'Chris Hounsom'
                            email = 'chrishounsom@icloud.com'
                        }
                        developer {
                            id = 'nicolae-leonte-go'
                            name = 'Nicolae Leonte'
                            email = 'nicolae.leonte.go@gmail.com'
                        }

                    }

                    scm {
                        connection = 'scm:git:https://github.com/melowe/tessera.git'
                        developerConnection = 'scm:git:https://github.com/melowe/tessera.git'
                        url = 'https://github.com/melowe/tessera'
                        tag = 'HEAD'
                    }

                    repositories {
                        maven {

                            //https://oss.sonatype.org/service/local/staging/deploy/maven2
                            // change URLs to point to your repos, e.g. http://my.org/repo
                            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'//"$buildDir/repos/releases"
                            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots' //"$buildDir/repos/snapshots"
                            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl


                            credentials {
                                if(project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                                    username = property('ossrhUsername')
                                    password = property('ossrhPassword')
                                }
                            }

                        }
                    }
                }
            }
        }
    }


    apply plugin: 'signing'

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    test {
        exclude '**/*IT.class'
    }

    jacocoTestCoverageVerification.dependsOn jacocoTestReport
    check.dependsOn checkstyleMain, checkstyleTest, spotlessCheck, jacocoTestCoverageVerification

}






